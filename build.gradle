buildscript {
    ext {
        springBootVer = "2.0.4.RELEASE"

        propdepsPluginVer = "0.0.10.RELEASE"
        versionPluginVer = "0.20.0"
        dockerPluginVer = "3.6.1"
    }

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "http://repo.spring.io/plugins-release" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVer"
        classpath "io.spring.gradle:propdeps-plugin:$propdepsPluginVer"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versionPluginVer"
        classpath "com.bmuschko:gradle-docker-plugin:$dockerPluginVer"
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "propdeps"
apply plugin: "propdeps-idea"
apply plugin: "com.github.ben-manes.versions"
apply plugin: "com.bmuschko.docker-spring-boot-application"

repositories {
    jcenter()
}

dependencies {

    compile "org.springframework.boot:spring-boot-starter-web"

    compile "org.springframework.boot:spring-boot-starter-actuator"
}

group = "com.swengin"
version = "0.1-SNAPSHOT"

sourceCompatibility = JavaVersion.VERSION_1_10
targetCompatibility = JavaVersion.VERSION_1_10

tasks.withType(JavaCompile) {
    options.fork = true
    options.incremental = true
}

bootJar {
    baseName = "bootiful-docker"
}

tasks.withType(Test) {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

wrapper {
    gradleVersion = "4.9"
    distributionType = Wrapper.DistributionType.ALL
}

docker {
    springBootApplication {
        baseImage = "openjdk:10-jre-slim"
        ports = [8080]
        tag = "swengin/bootiful-docker:0.0.1-SNAPSHOT"
    }
}

task stage(dependsOn: ["build", "clean"])